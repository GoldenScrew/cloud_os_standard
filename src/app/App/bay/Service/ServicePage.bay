/*!
 *  Bayrell Cloud Web Panel
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace App.Service;

use Runtime.lib;
use Runtime.MessageRPC;
use Runtime.Web.Annotations.Route;
use Runtime.Web.Annotations.RouteList;
use Runtime.Web.Annotations.RouteMiddleware;
use Runtime.Web.Events.Mouse.MouseClickEvent;
use Runtime.Web.Events.User.ChangeEvent;
use Runtime.Web.Component;
use Runtime.Web.LayoutModel;
use Runtime.Web.RenderContainer;
use Runtime.Web.RenderHelper;
use Runtime.Web.Response;
use Runtime.Web.CRUD.CrudEvent;
use Runtime.Web.CRUD.CrudInterface;
use Runtime.Web.CRUD.CrudModel;
use Runtime.Web.CRUD.ListPage;
use Runtime.Web.Button.Button;
use Runtime.Web.Dialog.Dialog;
use Runtime.Web.Dialog.DialogEvent;
use Runtime.Web.Dialog.DialogModel;
use App.Service.ServiceForm;
use App.Service.ServicePageModel;


@RouteList{}
@RouteMiddleware{ "value": "App.User.AuthToken::checkAuthRouteMiddleware" }
class ServicePage extends ListPage
{
	
	
	/**
	 * Returns module name
	 */
	pure string moduleName() => "App";
	
	
	
	/**
	 * Returns api name
	 */
	pure string crudApiName() => "App.Service";
	
	
	
	/**
	 * Returns form class name
	 */
	pure string getFormClassName() => classof ServiceForm;
	
	
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/services/", "name": "app.services.index" }
	static async RenderContainer actionIndex(RenderContainer container)
	{
		MessageRPC msg = container -> await method sendMessage
		{
			"api_name": static::crudApiName(),
			"space_name": classof CrudInterface,
			"method_name": "search",
			"data":
			{
			},
		};
		
		/* Get items */
		Collection items = msg::isSuccess(msg) ? msg["response"]["items"] : [];
		
		/* Set title */
		container <= layout <= title <= "Service";
		container <= layout <= layout_name <= "admin";
		
		/* Create model */
		container <= layout <= page_class <= classof ServicePage;
		container <= layout <= page_model <= ServicePageModel::create();
		container <= layout <= page_model <= items <= items != null ? items : [];
		container <= layout <= f_inc <= 1;
		
		/* Add top buttons */
		container = rtl::setAttr
		(
			container,
			["layout", "storage", "App.Layout.TopButtons"],
			[
				{
					"label": "Add",
					"component": classof ServicePage,
					"onClick": "onShowAddClick",
				}
			]
		);
		
		return container;
	}
	
	
	
	/**
	 * Render Top Buttons
	 */
	pure html renderTopButtons(LayoutModel layout, CrudModel model, Dict params) => "";
	
	
	
	/**
	 * Render item
	 */
	pure html renderItem(LayoutModel layout, CrudModel model, Dict params, Dict item) =>
		<div @class='item_row item_row--id'>
			ID: { rtl::attr(item, ["json", "ID"], "") }
		</div>
		<div @class='item_row item_row--name'>
			{ rtl::attr(item, ["json", "Spec", "Name"], "") }
		</div>
		<div @class='item_row item_row--image'>
			{
				static::trimImageName
				(
					rtl::attr(item, ["json", "Spec", "TaskTemplate", "ContainerSpec", "Image"], "")
				)
			}
		</div>
		<div @class='item_disabled'>
			{ item.get("enable", 0) == 0 ? _("App", "Disabled") : "" }
		</div>
	;
	
	
	
	/**
	 * Trim name
	 */
	pure string trimImageName(string name)
	{
		int pos = rs::search(name, "@");
		if (pos != -1) name = rs::substr(name, 0, pos);
		return name;
	}
	
	
	
	#ifdef FRONTEND then
	
	
	/**
	 * Dialog event
	 */
	async void onDialogEvent(DialogEvent e)
	{
		if (e.instance.params["@key"] == "dialog_answer")
		{
			if (e.button_result == "ok")
			{
				string action = e["tag"]["action"];
				Dict item = e["tag"]["item"];
				if (action == "enable")
				{
					await this.onEnableItem( item["id"] );
					return;
				}
				else if (action == "disable")
				{
					await this.onDisableItem( item["id"] );
					return;
				}
			}
		}
		
		await parent::onDialogEvent(e);
	}
	
	
	
	/**
	 * Form add Event
	 */
	async void onCrudEvent(CrudEvent e)
	{
		CrudModel model = this.model();
		
		if (e.event == "enable")
		{
			int index = model.active;
			Dict item = model -> method getItemByIndex(index);
			this.dialog_answer.show
			{
				"title": _("Runtime.Web.CRUD", "Enable %name% ?", {"name": model::getItemName(item)}),
				"tag": 
				{
					"action": "enable",
					"item": item
				},
			};
			return;
		}
		else if (e.event == "disable")
		{
			int index = model.active;
			Dict item = model -> method getItemByIndex(index);
			this.dialog_answer.show
			{
				"title": _("Runtime.Web.CRUD", "Disable %name% ?", {"name": model::getItemName(item)}),
				"tag": 
				{
					"action": "disable",
					"item": item
				},
			};
			return;
		}
		
		await parent::onCrudEvent(e);
	}
	
	
	/**
	 * Enable item
	 */
	async void onEnableItem(int item_id)
	{
		CrudModel model = this.model();
		string api_name = static::crudApiName();
		
		/* Set wait message */
		this.dialog_answer.updateModel
		{
			"message": _("Runtime.Web.CRUD", "Please wait ..."),
			"success": "",
			"error": "",
		};
		
		/* Call create api */
		MessageRPC msg = @ -> await method sendMessage
		{
			"api_name": api_name,
			"space_name": classof CrudInterface,
			"method_name": "enable",
			"data":
			{
				"id": item_id,
			},
		};
		
		/* Set result */
		bool is_success = msg -> method isSuccess;
		if (is_success)
		{
			CrudModel model = this.model();
			Dict item = msg["response"]["item"];
			
			/* Update item */
			int index = model.items.find( bool (Dict elem) use (item) => item["id"] == elem["id"] );
			this.updateModel
			{
				"items": model.items.setIm(index, item),
			};
			this.form_edit.updateModel
			{
				"item": item,
			}
			
			/* Hide dialog */
			this.dialog_answer.hide();
		}
		else
		{
			/* Set error message */
			this.dialog_answer.updateModel
			{
				"message": "",
				"success": "",
				"error": msg -> attr "error",
			};
		}
		
	}
	
	
	
	/**
	 * Disable item
	 */
	async void onDisableItem(int item_id)
	{
		CrudModel model = this.model();
		string api_name = static::crudApiName();
		
		/* Set wait message */
		this.dialog_answer.updateModel
		{
			"message": _("Runtime.Web.CRUD", "Please wait ..."),
			"success": "",
			"error": "",
		};
		
		/* Call create api */
		MessageRPC msg = @ -> await method sendMessage
		{
			"api_name": api_name,
			"space_name": classof CrudInterface,
			"method_name": "disable",
			"data":
			{
				"id": item_id,
			},
		};
		
		/* Set result */
		bool is_success = msg -> method isSuccess;
		if (is_success)
		{
			CrudModel model = this.model();
			Dict item = msg["response"]["item"];
			
			/* Update item */
			int index = model.items.find( bool (Dict elem) use (item) => item["id"] == elem["id"] );
			this.updateModel
			{
				"items": model.items.setIm(index, item),
			};
			this.form_edit.updateModel
			{
				"item": item,
			}
			
			/* Hide dialog */
			this.dialog_answer.hide();
		}
		else
		{
			/* Set error message */
			this.dialog_answer.updateModel
			{
				"message": "",
				"success": "",
				"error": msg -> attr "error",
			};
		}
		
	}
	
	#endif
	
}