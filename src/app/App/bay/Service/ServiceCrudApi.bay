/*!
 *  Bayrell Cloud Web Panel
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace App.Service;

use Runtime.lib;
use Runtime.MessageRPC;
use Runtime.Monad;
use Runtime.RuntimeUtils;
use Runtime.ORM.Cursor;
use Runtime.ORM.ORMProviderInterface;
use Runtime.Web.Annotations.ApiList;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Web.Annotations.Route;
use Runtime.Web.Annotations.RouteList;
use Runtime.Web.Cookie;
use Runtime.Web.JsonResponse;
use Runtime.Web.RenderContainer;
use Runtime.Web.Response;
use App.DockerLib;


@ApiList{ "space_name": "CrudInterface" }
class ServiceCrudApi
{
	
	/**
	 * Get Result
	 */
	pure fn getResult(MessageRPC msg, string s, int code) =>
		MessageRPC (Monad res) use (msg, s, code)
		{
			if (res.err == null and res.val === null)
				return new Monad
				(
					msg ->
						method fail(null, _("App", s), -1)
				);
			
			if (res.err != null)
				return new Monad
				(
					msg ->
						method fail(null, res.err.getErrorMessage(), res.err.getErrorCode())
				);
			
			/* Success */
			return new Monad
			(
				msg
					-> method success
					(
						res.val,
						res.val.get("Message", _("App", "Ok")), res.val.get("Code", 1)
					)
			);
		}
	;
	
	
	
	/**
	 * Search
	 */
	@ApiMethod{ "method_name": "search" }
	static async MessageRPC search(MessageRPC msg)
	{
		ORMProviderInterface p = @ -> method getProvider(classof ORMProviderInterface);
		
		string sql = "select * from " ~ p.prefix ~ "docker_services where is_deleted=0 order by id desc limit 100";
		Cursor cursor = p
			-> await method query(sql)
		;
		Collection<Dict> res = await cursor.fetchAll
		(
			Dict (Dict item)
			{
				item <= json <= RuntimeUtils::json_decode(item.json);
				return item;
			}
		);
		cursor.close();
		
		return msg -> method success(res);
	}
	
	
	
	/**
	 * Add service
	 */
	@ApiMethod{ "method_name": "add" }
	static async MessageRPC add(MessageRPC msg) =>
		msg
			-> attr "data"
			-> await DockerLib::dockerAddService
			-> monad static::getResult(msg, _("App", "Add error"), -1)
		;
	
	
	
	/**
	 * Update service
	 */
	@ApiMethod{ "method_name": "update" }
	static async MessageRPC update(MessageRPC msg) =>
		msg
			-> attr "data"
			-> await DockerLib::dockerUpdateService
			-> monad static::getResult(msg, _("App", "Update error"), -1)
		;
	
	
	
	/**
	 * Enable service
	 */
	@ApiMethod{ "method_name": "enable" }
	static async MessageRPC enable(MessageRPC msg) =>
		msg
			-> attr "data" -> attr "spec" -> attr "ID"
			-> await DockerLib::dockerEnableService
			-> monad static::getResult(msg, _("App", "Enable error"), -1)
		;
	
	
	
	/**
	 * Disable service
	 */
	@ApiMethod{ "method_name": "disable" }
	static async MessageRPC disable(MessageRPC msg) =>
		msg
			-> attr "data" -> attr "spec" -> attr "ID"
			-> await DockerLib::dockerDisableService
			-> monad static::getResult(msg, _("App", "Disable error"), -1)
		;
	
	
	
	/**
	 * Delete service
	 */
	@ApiMethod{ "method_name": "delete" }
	static async MessageRPC delete(MessageRPC msg) =>
		msg
			-> attr "data" -> attr "spec" -> attr "ID"
			-> await DockerLib::dockerDisableService
			
			-> monad lib::set( msg, true )
			-> attr "data" -> attr "spec" -> attr "ID"
			-> await DockerLib::databaseDeleteService
			
			-> monad lib::set( msg, true )
			-> attr "data" -> attr "service_id"
			-> await DockerLib::dockerDisableService
			
			-> monad lib::set( msg, true )
			-> attr "data" -> attr "service_id"
			-> await DockerLib::databaseDeleteService
			
			-> monad static::getResult(msg, _("App", "Delete error"), -1)
		;
	
}
