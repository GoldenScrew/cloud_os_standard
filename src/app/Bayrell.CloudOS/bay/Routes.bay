/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS;

use Runtime.Core.RemoteCallAnswer;
use Runtime.ORM.MySQL.Driver as MySQLDriver;
use Runtime.ORM.MySQL.Settings as MySQLSettings;
use Runtime.Web.Layout;
use Runtime.Web.LayoutModel;
use Runtime.Web.RenderContainer;
use Runtime.Web.RenderDriver;
use Runtime.Web.Response;
use Runtime.Web.Route;
use Runtime.Web.RouteList;
use Runtime.Web.CRUD.CrudPageModel;
use Runtime.Web.Table.TableModel;
use Bayrell.CloudOS.AdminLayout;
use Bayrell.CloudOS.Page404;
use Bayrell.CloudOS.Pattern;
use Bayrell.CloudOS.Dashboard.MainPage;
use Bayrell.CloudOS.Dashboard.MainPageModel;
use Bayrell.CloudOS.Other.AdminerPage;
use Bayrell.CloudOS.User.UsersPage;
use Bayrell.CloudOS.User.UsersPageModel;


@RouteList{}
static class Routes
{
	
	/**
	 * Layout chain
	 */
	pure LayoutModel layoutChain(LayoutModel layout)
	{
		if (layout.layout_name == "default") layout <= layout_class <= classof AdminLayout;
		return [ layout ];
	}
	
	
	
	/**
	 * Render chain. Set default pattern
	 */
	pure RenderContainer renderChainPatternDefault(RenderContainer container)
	{
		if (container.pattern_name == "default" and container.pattern_class == "" or container.pattern_class == "")
		{
			container <= pattern_class <= classof Pattern;
		}
		return [ container ];
	}
	
	
	
	/**
	 * Render chain. Route after
	 */
	pure RenderContainer renderChainRouteAfter(RenderContainer container)
	{
		/* Change title */
		container <= layout <= title <= container.layout.title ~ " | BAYRELL Cloud OS";
		
		return [ container ];
	}
	
	
	
	/**
	 * Route Action
	 * @return RenderContainer
	 */
	@Route{ "uri": "/", "name": "site:index" }
	static async RenderContainer MainPage(RenderContainer container)
	{
		/*
		RemoteCallAnswer answer = await RenderDriver::remoteBusCall
		(
			{
				"object_name": "App.Dashboard",
				"method_name": "status",
			},
			container
		);
		*/
		MainPageModel page_model = new MainPageModel
		{
			/*"services": answer.isSuccess() ? msg["response"]["services"] : null,*/
		};
		
		/* Set title */
		container <= layout <= title <= "Index page";
		container <= layout <= layout_name <= "default";
		
		/* Set model */
		container <= layout <= page_class <= classof MainPage;
		container <= layout <= page_model <= page_model;
		
		return [ container ];
	}
	
	
	
	/**
	 * Route Action
	 * @return RenderContainer
	 */
	@Route{ "uri": "/users/", "name": "site:users" }
	static async RenderContainer UsersPage(RenderContainer container)
	{
		/* Create model */
		UsersPageModel page_model = new UsersPageModel
		{
			"crud": new CrudPageModel
			{
			},
		};
		
		/* Remote call */
		Dict search_params = RenderDriver::getCrudSearchParams(container.request);
		RemoteCallAnswer answer = await RenderDriver::remoteBusCall
		(
			{
				"object_name": "Bayrell.CloudOS.Users",
				"interface_name": "core.crud",
				"method_name": "search",
				"data": search_params,
			},
			container
		);
		
		/* Answer */
		if (answer.isSuccess())
		{
			page_model <= crud <= table <= rows <= answer.response["items"];
			page_model <= crud <= table <= page <= answer.response["page"];
			page_model <= crud <= table <= pages <= answer.response["pages"];
		}
		
		/* Set title */
		container <= layout <= title <= "Users page";
		container <= layout <= layout_name <= "default";
		
		/* Set model */
		container <= layout <= page_class <= classof UsersPage;
		container <= layout <= page_model <= page_model;
		
		return [ container ];
	}
	
	
	
	/**
	 * Render chain
	 * Page not found
	 * @return RenderContainer
	 */
	static async RenderContainer Page404(RenderContainer container)
	{
		if (container == null or container.isResponseExists() or container.isPageExists()) return [ container ];
		
		/* Set title */
		container <= layout <= title <= "Page not found";
		container <= layout <= layout_name <= "default";
		
		/* Set model */
		container <= layout <= page_class <= classof Page404;
		container <= layout <= page_model <= null;
		
		/* Set 404 code */
		container <= new_http_code <= 404;
		
		return [ container ];
	}
	
	
	
	#ifdef BACKEND then
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/adminer/", "name": "app.adminer.index" }
	static async RenderContainer adminerPage(RenderContainer container)
	{
		MySQLSettings settings = @.config(["Runtime.ORM.MySQL", "default"]);
		
		/* Set title */
		container <= layout <= title <= "Adminer page";
		
		/* Create model */
		container <= layout <= page_class <= classof AdminerPage;
		container <= layout <= page_model <= new Dict
		{
			"db": settings,
		};
		
		return [ container ];
	}
	
	
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/adminer/{version}/", "name": "app.adminer.version" }
	static async RenderContainer adminerVersionPage(RenderContainer container)
	{
		string content = "";
		string version = container.route_params.get("version", "");
		
		#ifcode PHP then
		$file_path = \Runtime\rtl::getModulePath($ctx, "Bayrell.CloudOS") . "/data/adminer-4.7.6.php";
		@ob_start();
		include $file_path;
		$content = ob_get_contents();
		@ob_end_clean();
		#endif
		
		container <= response <= new Response{ "content": content };
		
		return [ container ];
	}
	
	#endif
}