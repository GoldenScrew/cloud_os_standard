<!--
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
-->

<class version="1.0"
	name="Bayrell.CloudOS.Design.SpacesPage" extends="Runtime.Web.CRUD.CrudPage" 
	module="Bayrell.CloudOS" model="Runtime.Web.CRUD.CrudPageModel"
>

<use name="Runtime.lib" />
<use name="Runtime.Exceptions.RuntimeException" />
<use name="Runtime.Core.Message" />
<use name="Runtime.Core.RemoteCallAnswer" />
<use name="Runtime.Web.LayoutModel" />
<use name="Runtime.Web.RenderContainer" />
<use name="Runtime.Web.RenderDriver" />
<use name="Runtime.Web.Route" />
<use name="Runtime.Web.RouteList" />
<use name="Runtime.Web.RouteMiddleware" />
<use name="Runtime.Web.Events.MouseClickEvent" />
<use name="Runtime.Web.CRUD.CrudPage" component="true" />
<use name="Runtime.Web.CRUD.CrudPageModel" />
<use name="Runtime.Web.CRUD.FieldInfo" />
<use name="Runtime.Web.Input.Input" component="true" />
<use name="Runtime.Web.Input.Label" component="true" />
<use name="Runtime.Web.Input.Select" component="true" />
<use name="Runtime.Web.Input.SelectText" component="true" />
<use name="Runtime.Web.Table.TableModel" />


<script>


/**
 * Returns object name
 */
pure string getCrudObjectName() => "Bayrell.CloudOS.Space";



/**
 * Route Action
 * @return RenderContainer
 */
@Route{ "uri": "/admin/spaces/", "name": "app.admin.spaces" }
@RouteMiddleware{ "value": "Runtime.Web.Auth.AuthFrontend::checkAuthMiddleware" }
static async RenderContainer actionIndex(RenderContainer container)
{
	/* Create model */
	CrudPageModel page_model = await CrudPageModel::crudSearch(static::getCrudObjectName(), container);
	
	/* Set title */
	container <= layout <= title <= "Spaces";
	container <= layout <= layout_name <= "admin";
	
	/* Set model */
	container <= layout <= page_class <= classof SpacesPage;
	container <= layout <= page_model <= page_model;
	
	return [ container ];
}



/**
 * Returns options
 */
pure Collection getOptions(LayoutModel layout, CrudPageModel model, Dict params, string name = "")
{
	if (name == "domains")
		return model
			|> attr "foreigns", "domains", "options"
			|> lib::map
			(
				Dict (Dict item) =>
				{
					"id": item["domain_name"],
					"value": item["domain_name"],
					"item": item,
				}
			)
			|> default Collection []
		;
	
	return parent::getOptions(layout, model, params, name);
}



/**
 * Returns crud struct
 */
pure Collection<FieldInfo> getStruct(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	new FieldInfo
	{
		"api_name": "space_id",
		"primary": true,
	},
	
	new FieldInfo
	{
		"api_name": "number",
		"label": "",
		"class_name": classof Label,
		"info":
		{
			"table":
			{
				"render": method static::renderNumber,
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "space_name",
		"label": "Space name",
		"class_name": classof Input,
		"info":
		{
			"table":
			{
				"class_name": classof Label,
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "domain_name",
		"label": "Domain name",
		"class_name": classof Select,
		"class_settings":
		{
			"show_select_value": true,
			"options": static::getOptions(layout, model, params, "domains"),
		},
		"info":
		{
			"table":
			{
				"class_name": classof SelectText,
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "edit-buttons",
		"label": "",
		"class_name": classof Label,
		"info":
		{
			"table":
			{
				"render": method static::renderButtons,
			},
		},
	},
];



/**
 * Returns filter fields
 */
pure Collection<string> getFilterFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	"space_name",
	"domain_name",
];



/**
 * Returns form fields
 */
pure Collection<string> getFormFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	"space_name",
	"domain_name",
];



/**
 * Returns table fields
 */
pure Collection<string> getTableFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	"number",
	"space_name",
	"domain_name",
	"edit-buttons"
];



/**
 * Returns view fields
 */
pure Collection<string> getViewFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
];



/**
 * Returns messages
 */
pure Dict<string> getMessages(LayoutModel layout, CrudPageModel model, Dict params) =>
{
	"add": _("Bayrell.TimePlanner", "Add space"),
	"delete": string (Dict item) =>
		_("Runtime.Web.CRUD", "Do you realy want to delete '%name%' ?", { "name": item["space_name"] } ),
};



/**
 * Dialog settings
 */
pure Dict getDialogSettings(LayoutModel layout, CrudPageModel model, Dict params) =>
{
};



/**
 * Form settings
 */
pure Dict getFormSettings(LayoutModel layout, CrudPageModel model, Dict params) =>
{
};


</script>



</class>