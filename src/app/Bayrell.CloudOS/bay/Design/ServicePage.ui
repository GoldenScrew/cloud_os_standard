<!--
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
-->

<class version="1.0"
	name="Bayrell.CloudOS.Design.ServicePage" extends="Runtime.Web.CRUD.CrudPage" 
	module="Bayrell.CloudOS" model="Runtime.Web.CRUD.CrudPageModel"
>

<use name="Runtime.lib" />
<use name="Runtime.Exceptions.RuntimeException" />
<use name="Runtime.Core.Message" />
<use name="Runtime.Core.RemoteCallAnswer" />
<use name="Runtime.Web.LayoutModel" />
<use name="Runtime.Web.RenderContainer" />
<use name="Runtime.Web.RenderDriver" />
<use name="Runtime.Web.Route" />
<use name="Runtime.Web.RouteList" />
<use name="Runtime.Web.RouteMiddleware" />
<use name="Runtime.Web.Events.MouseClickEvent" />
<use name="Runtime.Web.CRUD.CrudPage" component="true" />
<use name="Runtime.Web.CRUD.CrudPageModel" />
<use name="Runtime.Web.CRUD.FieldInfo" />
<use name="Runtime.Web.CRUD.FormModel" />
<use name="Runtime.Web.CRUD.TableModel" />
<use name="Runtime.Web.Input.Input" component="true" />
<use name="Runtime.Web.Input.Label" component="true" />
<use name="Runtime.Web.Input.Select" component="true" />
<use name="Runtime.Web.Input.SelectText" component="true" />


<script>


/**
 * Returns object name
 */
pure string getCrudObjectName() => "Bayrell.CloudOS.Service";



/**
 * Route Action
 * @return RenderContainer
 */
@Route{ "uri": "/admin/services/", "name": "app.admin.services" }
@RouteMiddleware{ "value": "Runtime.Web.Auth.AuthFrontend::checkAuthMiddleware" }
static async RenderContainer actionIndex(RenderContainer container) => [
	container
		
		/* Set title */
		|> lib::setAttr(["layout", "title"], "Services")
		|> lib::setAttr(["layout", "layout_name"], "admin")
		
		/* Set model */
		|> lib::setAttr(["layout", "page_class"], classof ServicePage)
		|> lib::setAttr(["layout", "page_model"], new CrudPageModel())
		
		/* Make api request */
		|> await static::crudSearch
];



/**
 * Returns options
 */
pure Collection getOptions(RenderContainer container, string name = "")
{
	return parent::getOptions(container, name);
}



/**
 * Returns crud struct
 */
pure Collection<FieldInfo> getStruct(RenderContainer container) =>
[
	new FieldInfo
	{
		"api_name": "service_id",
		"primary": true,
	},
	
	new FieldInfo
	{
		"api_name": "number",
		"label": "",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "stack_name",
		"label": "Stack name",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "service_name",
		"label": "Service name",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "software_api_name",
		"label": "Software api name",
		"class_name": classof Input,
		"info":
		{
			"table":
			{
				"class_name": classof Label
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "docker_name",
		"label": "Docker name",
		"class_name": classof Label,
		"info":
		{
			"filter":
			{
				"class_name": classof Input
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "docker_replicas",
		"label": "Replicas",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "docker_image",
		"label": "Docker image",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "have_admin_page",
		"label": "Admin page",
		"class_name": classof Select,
		"class_settings":
		{
			"show_select_value_filter": true,
			"show_select_value": false,
			"options":
			[
				{"id": 0, "value": "No"},
				{"id": 1, "value": "Yes"},
			],
		},
		"info":
		{
			"table":
			{
				"class_name": classof SelectText,
			},
			"form":
			{
				"class_settings":
				{
					"default": 0,
				},
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "admin_page_settings",
		"label": "Admin settings",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "admin_port",
		"label": "Admin port",
		"class_name": classof Input,
	},
	
	new FieldInfo
	{
		"api_name": "admin_route",
		"label": "Admin route",
		"class_name": classof Input,
	},
	
	new FieldInfo
	{
		"api_name": "enable",
		"label": "Enable",
		"class_name": classof Select,
		"class_settings":
		{
			"show_select_value_filter": true,
			"show_select_value": false,
			"options":
			[
				{"id": 0, "value": "No"},
				{"id": 1, "value": "Yes"},
			],
		},
		"info":
		{
			"table":
			{
				"class_name": classof SelectText,
			},
			"form":
			{
				"class_settings":
				{
					"default": 0,
				},
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "edit-buttons",
		"label": "",
		"class_name": classof Label,
	},
];



/**
 * Returns filter fields
 */
pure Collection<string> getFilterFields(RenderContainer container) =>
[
	"docker_name",
	"have_admin_page",
	"enable",
];



/**
 * Returns form fields
 */
pure Collection<string> getFormFields(RenderContainer container) =>
[
	"stack_name",
	"service_name",
	"software_api_name",
	"have_admin_page",
	"admin_port",
	"admin_route",
];



/**
 * Returns table fields
 */
pure Collection<string> getTableFields(RenderContainer container) =>
[
	"number",
	"docker_name",
	"docker_image",
	"software_api_name",
	"docker_replicas",
	"enable",
	"have_admin_page",
	"edit-buttons"
];



/**
 * Returns view fields
 */
pure Collection<string> getViewFields(RenderContainer container) =>
[
];



/**
 * Returns messages
 */
pure Dict<string> getMessages() =>
{
	"add": _("Bayrell.TimePlanner", "Add service"),
	"delete": string (Dict item) =>
		_("Runtime.Web.CRUD", "Do you realy want to delete '%name%' ?", { "name": item["docker_name"] } ),
};



/**
 * Dialog settings
 */
pure Dict getDialogSettings() =>
{
};



/**
 * Form settings
 */
pure Dict getFormSettings() =>
{
};



/**
 * Custom class name
 */
pure string customClassName(LayoutModel layout, string kind, string field_name, var value, Dict crud_settings)
{
	if (kind == "form" and field_name == "service_name")
	{
		FormModel model = crud_settings["crud_form_model"];
		return model["action"] == "create" ? classof Input : classof Label;
	}
	
	if (kind == "form" and field_name == "stack_name")
	{
		FormModel model = crud_settings["crud_form_model"];
		return model["action"] == "create" ? classof Input : classof Label;
	}
	
	return crud_settings["crud_class_name"];
}



/**
 * Custom field render
 */
pure html calcField(LayoutModel layout, string kind, string field_name, var value, Dict crud_settings)
{
	if (field_name == "docker_image")
	{
		Dict item = crud_settings["crud_item"];
		string image = item["docker_image"];
		return static::trimImageName(image);
	}
	
	if (field_name == "docker_replicas")
	{
		Dict item = crud_settings["crud_item"];
		int work = item["docker_balancer", "State", "Work"] |> default int 0;
		int total = item["docker_balancer", "State", "Total"] |> default int 0;
		return work ~ " / " ~ total;
	}
	
	return value;
}



/**
 * Custom field render
 */
pure html renderField(LayoutModel layout, string kind, string field_name, var value, Dict crud_settings)
{
	Dict item = crud_settings["crud_item"];
	
	/* Table number */
	if (kind == "table" and field_name == "number")
		return static::renderNumber(layout, value, crud_settings);
	
	/* Table have admin page button */
	if (kind == "table" and field_name == "have_admin_page")
		return (value == 0)
			? "No"
			:
			<div>
				<div style='display:inline-block;vertical-align: middle; margin-right: 5px;'>
				{ static::renderButton
				(
					layout, crud_settings, "view",
					{
						"url": "/admin/services/" ~ crud_settings["crud_item"]["docker_name"] ~ "/",
						"target": "_blank",
					}
				) }
				</div>
				<div style='display:inline-block;vertical-align: middle; margin-left: 5px;'>
					<div>Port = { item["admin_port"] }</div>
					<div>Route = { item["admin_route"] }</div>
				</div>
			</div>
		;
	
	/* Table edit buttons */
	if (kind == "table" and field_name == "edit-buttons")
		return static::renderButtons(layout, value, crud_settings);
	
	/* Admin page settings */
	if (kind == "table" and field_name == "admin_page_settings")
	{
		return "";
	}
	
	return null;
}



/**
 * Trim name
 */
pure string trimImageName(string name)
{
	int pos = rs::search(name, "@");
	if (pos != -1) name = rs::substr(name, 0, pos);
	return name;
}


</script>



</class>